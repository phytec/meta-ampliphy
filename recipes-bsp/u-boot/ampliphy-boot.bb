# Copyright (C) 2025 PHYTEC Messtechnik GmbH
# Author: Wadim Egorov <w.egorov@phytec.de>
#
# This recipe provides U-Boot scripts for booting Ampliphy. It is intended
# for use with U-Boot's Standard Boot framework:
#
#   https://docs.u-boot.org/en/latest/develop/bootstd/overview.html
#
# In order for your board to be able to boot Ampliphy, you have to make sure
# your board provides all neccessary variables. The scripts generated by
# amliphy-boot.bb makes use of the environment variable conventions
# documented in:
#
#   https://docs.u-boot.org/en/latest/develop/bootstd/overview.html#environment-variables
#
# The final boot script file is generated with mkimage and packed into a FIT.
#
#
# Other Environment Variables used:
#
# fit_addr_r (optional)
#   In case if the default $loadaddr is not suitabale for loading a fitImage
# overlaysenvfile (optional)
#   Name of the env file on the boot partition containing default overlays if
#   default "overlays.txt" is not suitabale.

DESCRIPTION = "This recipe compiles U-Boot scripts into FIT images, which are \
loaded by U-Boot using the standard boot script method. These scripts provide \
a consistent boot process for the Ampliphy distribution, ensuring compatibility \
across different platforms."
SUMMARY = "U-Boot scripts for booting Ampliphy"
LICENSE = "MIT"
LIC_FILES_CHKSUM = "file://${COMMON_LICENSE_DIR}/MIT;md5=0835ade698e0bcf8506ecda2f7b4f302"

DEPENDS = "u-boot-mkimage-native dtc-native"
DEPENDS:append:secureboot = " libp11-native"

SRC_URI = " \
    file://mmc_boot.cmd \
    file://spi_boot_fit.cmd.in \
    file://mmc_boot_fit.cmd \
    file://mmc_boot_fit_secure.cmd \
    file://net_boot_fit.cmd \
    file://boot.its.in \
    file://boot-sign.its.in \
"

inherit deploy secureboot

# default bootscript (boot.scr.uimg) to be built into the .wic image
DEFAULT_BOOTSCRIPT ??= "mmc_boot.cmd"
DEFAULT_BOOTSCRIPT:secureboot ?= "mmc_boot_fit_secure.cmd"
DEFAULT_BOOTSCRIPT:mx8-generic-bsp ?= "mmc_boot_fit.cmd"
DEFAULT_BOOTSCRIPT:mx9-generic-bsp ?= "mmc_boot_fit.cmd"

BOOTSCRIPTS ??= "*.cmd"
BOOTSCRIPTS:mx8-generic-bsp ?= "mmc_boot.cmd mmc_boot_fit.cmd mmc_boot_fit_secure.cmd net_boot_fit.cmd"
BOOTSCRIPTS:mx9-generic-bsp ?= "mmc_boot.cmd mmc_boot_fit.cmd mmc_boot_fit_secure.cmd net_boot_fit.cmd"

DEFAULT_ITS ??= "boot.its.in"
DEFAULT_ITS:secureboot ?= "boot-sign.its.in"

# Used by the spi boot script to locate the fitImage
# Only set fitImage location here. The rest should be set in U-Boot
SPI_MTD_PARTS ?= ""
SPI_MTD_PARTS:k3 ?= "nor0:-@0x740000(fitimage)"

S = "${UNPACKDIR}"

do_compile() {
    sed -e 's/@@SPI_MTD_PARTS@@/${SPI_MTD_PARTS}/' "${S}/spi_boot_fit.cmd.in" > spi_boot_fit.cmd

    for script in ${BOOTSCRIPTS} ; do
        sed -e "s/@@BOOTCOMMAND_FILE@@/${script}/" \
            -e "s/@@UBOOT_SIGN_KEYNAME@@/${UBOOT_SIGN_KEYNAME}/" \
            -e "s/@@UBOOT_SIGN_IMG_KEYNAME@@/${UBOOT_SIGN_IMG_KEYNAME}/" \
            -e "s/@@FIT_SIGN_ALG@@/${FIT_SIGN_ALG}/" \
            -e "s/@@FIT_HASH_ALG@@/${FIT_HASH_ALG}/" \
            "${S}/${DEFAULT_ITS}" > boot.its
        mkimage -C none -A ${UBOOT_ARCH} -f boot.its ${script}.scr.uimg
        if [ "${UBOOT_SIGN_ENABLE}" = "1" ] && [ "${FIT_SIGN_INDIVIDUAL}" = "1" ]; then
            if echo "${UBOOT_SIGN_KEYDIR}" | grep -q "pkcs11:"; then
                setup_pkcs11_env
            fi
            mkimage ${UBOOT_MKIMAGE_SIGN_ARGS} \
                -k ${UBOOT_SIGN_KEYDIR} -F ${script}.scr.uimg
        fi
    done
}

do_deploy() {
    install -d ${DEPLOYDIR}

    for cmdimg in *.uimg ; do
        img=$(echo "$cmdimg" | sed 's/.cmd//')  # drop .cmd
        install -m 0644 ${cmdimg} "${DEPLOYDIR}/${img}"
    done

    install -m 0644 ${S}/${DEFAULT_BOOTSCRIPT}.scr.uimg ${DEPLOYDIR}/boot.scr.uimg
}

addtask deploy after do_install before do_build
